<h1 id="user-defined-types-and-io">User defined types and I/O</h1>
<p>Formatted output is accomplished using edit descriptors as you’ve
seen before. A large number of descriptors is available for Fortran’s
built-in types. However, it is also possible to define edit descriptors
for user defined types, although it is not quite trivial. This is called
derived type I/O.</p>
<p>Consider a type that represents quantities required for descriptive
statistics (mean value, standard deviation) on streaming data. The
type’s elements are shown below.</p>
<pre class="fortran"><code>type, public :: descriptive_stats_t
    private
    integer :: nr_values = 0
    real :: sum = 0.0, sum2 = 0.0
contains
    procedure, public, pass :: add_value, get_nr_values, get_mean, get_stddev
end type descriptive_stats_t</code></pre>
<p>The type bound procedure <code>add_value</code> will update the
variable’s elements <code>nr_values</code>, <code>sum</code> and
<code>sum2</code> with a new, incoming data value. The functions
<code>get_mean</code> and <code>get_stddev</code> return the current
value of the mean and standard deviation of the data processed so far.
The function <code>get_nr_values</code> returns the number of data
values seen so far.</p>
<p>It would be convenient to have a formatting code for a value of this
user defined type. The first step towards this is to define a procedure
to produce the appropriate output.</p>
<pre class="fortran"><code>subroutine write_stats(stats, unit_nr, iotype, v_list, iostat, iomsg)
    implicit none
    class(descriptive_stats_t), intent(in) :: stats
    integer, intent(in) :: unit_nr
    character(len=*), intent(in) :: iotype
    integer, dimension(:), intent(in) :: v_list
    integer, intent(out) :: iostat
    character(len=*), intent(inout) :: iomsg
    character(len=1024) :: fmt_str

    if (size(v_list) == 4) then
        write (fmt_str, '(A, 2(A, I0, A, I0), A)') &amp;
            '("n = ", I0', &amp;
            ', ", mean = ", F', v_list(1), '.', v_list(2), &amp;
            ', ", stddev = ", F', v_list(3), '.', v_list(4), &amp;
            ')'
        write (unit=unit_nr, fmt=fmt_str, iostat=iostat, iomsg=iomsg) &amp;
            stats%get_nr_values(), stats%get_mean(), stats%get_stddev()
    else
        write (unit=unit_nr, fmt=*, iostat=iostat, iomsg=iomsg) &amp;
            stats%get_nr_values(), stats%get_mean(), stats%get_stddev()
    end if
end subroutine write_stats</code></pre>
<p>The first argument <code>stats</code> of the subroutine is the value
of this user defined type that you want to print. The second argument,
<code>unit_nr</code> is the unit you want to write to. The
<code>iostat</code> argument’s value is typically ignored, it is mostly
present for compatibility with unformatted I/O. The fourth argument,
<code>v_list</code> is an array that contains the arguments to the edit
descriptor when it is used in a format. In this case, it can have 4
values, the width and number of digits to write for the mean value, and
similar for the standard deviation. This is a very simple example, so if
the size of the <code>v_list</code> array is not equal to 4 values, we
use defaults. The last two arguments will be set to the I/O status, and
in case there is a problem, the I/O message of the underlying write
statements.</p>
<p>Either a format string is built based on the values in
<code>v_list</code>, or a default is used to write the values to the
destination unit.</p>
<p>The next step is to ensure that this subroutine will be used to print
values of this type, and that can be done by adding it to the type.</p>
<pre class="format"><code>type, public :: descriptive_stats_t
    private
    integer :: nr_values = 0
    real :: sum = 0.0, sum2 = 0.0
contains
    procedure, public, pass :: add_value, get_nr_values, get_mean, get_stddev
    procedure, private :: write_stats
    generic :: write(formatted) =&gt; write_stats
end type descriptive_stats_t</code></pre>
<p>The subroutine <code>write_stats</code> is a type bound procedure and
it is declared Private since it is not intended to be called directly.
It is bound to a formatted write operation using generic.</p>
<p>Finally, you can use the edit descriptor in a format string for a
write statement, as for instance in the program compilation unit
below.</p>
<pre class="fortran"><code>program descriptive_statistics
    use, intrinsic :: iso_fortran_env, only : error_unit, input_unit, output_unit
    use :: stats_mod
    implicit none
    type(descriptive_stats_t) :: stats
    integer :: istat
    character(len=2048) :: msg
    real :: data_value

    do
        read (unit=input_unit, fmt=*, iostat=istat, iomsg=msg) data_value
        if (istat == -1) exit
        if (istat /= 0) then
            write (unit=error_unit, fmt='(2A)') 'warning: ', trim(msg)
            cycle
        end if
        call stats%add_value(data_value)
    end do
    print '(A, I0)', 'n      = ', stats%get_nr_values()
    print '(A, F10.3)', 'mean   = ', stats%get_mean()
    print '(A, F10.3)', 'stddev = ', stats%get_stddev()
    write (unit=output_unit, fmt='(dt)') stats
    print '(dt(7, 2, 7, 2))', stats

end program descriptive_statistics</code></pre>
<p>The <code>dt</code> edit descriptor is used with 4 arguments, the
width and number of digits for the mean value, and the standard
deviation respectively.</p>
<p>It is equally possible to write a subroutine to read a user defined
type, but that is typically fairly brittle, so we won’t go into that
here.</p>
